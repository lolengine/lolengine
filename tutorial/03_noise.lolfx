-- GLSL.Vert --

#version 120

attribute vec2 in_Position;

void main(void)
{
    gl_Position = vec4(in_Position, 0.0, 1.0);
}

-- GLSL.Frag --

#version 120

uniform float in_Time;

vec3 noise3(vec2 p)
{
    return vec3(fract(sin(dot(p, vec2(12.3453, -54.3353432))) * 8433.235443),
                fract(sin(dot(p, vec2(34.23232, 98.03537))) * 8294.5345433),
                fract(sin(dot(p, vec2(82.91234, -19.243533))) * 2912.454369));
}

vec3 noise3(vec3 p)
{
    return vec3(fract(sin(54.343909 + dot(p, vec3(12.3453, -54.3353432, 34.3490432))) * 8433.235443),
                fract(sin(93.329945 - dot(p, vec3(34.23232, 98.0353743, 90.2390984))) * 8294.5345433),
                fract(sin(32.039284 + dot(p, vec3(82.91234, -19.243533, 22.3294090))) * 2912.454369));
}

vec3 perlin2d(vec2 p)
{
    vec2 a = floor(p);
    vec2 d = p - a;
    d = d * d * (3.0 - 2.0 * d);

    vec3 n1 = noise3(a);
    vec3 n2 = noise3(a + vec2(1.0, 0.0));
    vec3 n3 = noise3(a + vec2(0.0, 1.0));
    vec3 n4 = noise3(a + vec2(1.0, 1.0));

    vec3 q1 = n2 * d.x + n1 * (1.0 - d.x);
    vec3 q2 = n4 * d.x + n3 * (1.0 - d.x);

    vec3 q3 = q2 * d.y + q1 * (1.0 - d.y);

    return q3;
}

vec3 perlin3d(vec3 p)
{
    mat3 r = mat3(0.36, 0.48, -0.8, -0.8, 0.60, 0.0, 0.48, 0.64, 0.60);
    p = r * p;

    vec3 a = floor(p);
    vec3 d = p - a;
    d = d * d * (3.0 - 2.0 * d);

    vec3 o1 = noise3(a);
    vec3 o2 = noise3(a + vec3(1.0, 0.0, 0.0));
    vec3 o3 = noise3(a + vec3(0.0, 1.0, 0.0));
    vec3 o4 = noise3(a + vec3(1.0, 1.0, 0.0));
    vec3 o5 = noise3(a + vec3(0.0, 0.0, 1.0));
    vec3 o6 = noise3(a + vec3(1.0, 0.0, 1.0));
    vec3 o7 = noise3(a + vec3(0.0, 1.0, 1.0));
    vec3 o8 = noise3(a + vec3(1.0, 1.0, 1.0));

    vec3 n1 = o2 * d.x + o1 * (1.0 - d.x);
    vec3 n2 = o4 * d.x + o3 * (1.0 - d.x);
    vec3 n3 = o6 * d.x + o5 * (1.0 - d.x);
    vec3 n4 = o8 * d.x + o7 * (1.0 - d.x);

    vec3 q1 = n2 * d.y + n1 * (1.0 - d.y);
    vec3 q2 = n4 * d.y + n3 * (1.0 - d.y);

    vec3 q3 = q2 * d.z + q1 * (1.0 - d.z);

    return q3;
}

void main(void)
{
    vec2 xy = gl_FragCoord.xy;
    float z = in_Time * 2.0;

    vec3 p = perlin3d(vec3(xy / 160.0, z)) / 2.0
           + perlin3d(vec3(xy / 80.0, z + 1.5)) / 4.0
           + perlin3d(vec3(xy / 40.0, z + 0.3)) / 8.0
           + perlin3d(vec3(xy / 20.0, z + 3.3)) / 16.0
           + perlin3d(vec3(xy / 10.0, z + 2.0)) / 32.0
           + perlin3d(vec3(xy / 5.0, z + 1.7)) / 64.0
           + perlin3d(vec3(xy / 2.5, z + 4.2)) / 128.0;
    p = p * p * (3.0 - 2.0 * p);
    p = p * p * (3.0 - 2.0 * p);
    gl_FragColor = vec4(min(p.x * 2.0, 1.0), max(p.x * 1.5 - 0.5, 0.0), 0.0, 1.0);
}

-- HLSL.Vert --

void main(float2 in_Position : POSITION,
          out float4 out_Position : POSITION)
{
    out_Position = float4(in_Position, 0.0, 1.0);
}

-- HLSL.Frag --

void main(out float4 out_FragColor : COLOR)
{
    out_FragColor = float4(0.7, 0.2, 0.5, 1.0);
}

