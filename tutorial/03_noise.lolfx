-- GLSL.Vert --

#version 120

attribute vec2 in_Position;

void main(void)
{
    gl_Position = vec4(in_Position, 0.0, 1.0);
}

-- GLSL.Frag --

#version 120

uniform float u_Time;

vec3 mod289(vec3 x)
{
    return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec3 perm(vec3 x)
{
    return mod289(((x * 34.0) + 1.0) * x);
}

vec4 noise3d(vec3 p1, vec3 p2, vec3 p3, vec3 p4)
{
    p1 = perm(perm(perm(p1) + p1.zxy) + p1.yzx);
    p2 = perm(perm(perm(p2) + p2.zxy) + p2.yzx);
    p3 = perm(perm(perm(p3) + p3.zxy) + p3.yzx);
    p4 = perm(perm(perm(p4) + p4.zxy) + p4.yzx);
    return fract(vec4(p1.x, p2.x, p3.x, p4.x) / 41.0);
}

float perlin3d(vec3 p)
{
    mat3 r = mat3(0.36, 0.48, -0.8, -0.8, 0.60, 0.0, 0.48, 0.64, 0.60);
    p = r * p;

    vec3 a = floor(p);
    vec3 d = p - a;
    d = d * d * (3.0 - 2.0 * d);

    vec4 o1 = noise3d(a,
                      a + vec3(0.0, 1.0, 0.0),
                      a + vec3(0.0, 0.0, 1.0),
                      a + vec3(0.0, 1.0, 1.0));
    vec4 o2 = noise3d(a + vec3(1.0, 0.0, 0.0),
                      a + vec3(1.0, 1.0, 0.0),
                      a + vec3(1.0, 0.0, 1.0),
                      a + vec3(1.0, 1.0, 1.0));

    vec4 o3 = o2 * d.x + o1 * (1.0 - d.x);
    vec2 o4 = o3.yw * d.y + o3.xz * (1.0 - d.y);

    return o4.y * d.z + o4.x * (1.0 - d.z);
}

void main(void)
{
    vec2 xy = gl_FragCoord.xy * 2.0;
    xy.y -= u_Time * 400.0;
    float z = u_Time * 2.0;

    float p = perlin3d(vec3(xy / 80.0, z + 1.5)) / 2.0
            + perlin3d(vec3(xy / 40.0, z + 0.3)) / 4.0
            + perlin3d(vec3(xy / 20.0, z + 3.3)) / 8.0
            + perlin3d(vec3(xy / 10.0, z + 2.0)) / 16.0;

    /* Scroll by adding [-.5,.5] */
    p -= gl_FragCoord.y / 720.0 - 0.5;
    p = max(p, 0.0);
    p = min(p, 1.0);

    float q = p * p * (3.0 - 2.0 * p);
    float r = q * q * (3.0 - 2.0 * q);
    gl_FragColor = vec4(min(q * 2.0, 1.0),
                        max(r * 1.5 - 0.5, 0.0),
                        max(q * 8.0 - 7.3, 0.0),
                        1.0);
}

-- HLSL.Vert --

void main(float2 in_Position : POSITION,
          uniform float2 u_WinSize,
          out float4 out_Position : POSITION,
          out float2 pass_Position : TEXCOORD0)
{
    //pass_Position = in_Position * u_WinSize;
    pass_Position = in_Position * float2(1280.0, 720.0);
    out_Position = float4(in_Position, 0.0, 1.0);
}

-- HLSL.Frag --

float3 mod289(float3 x)
{
    return x - floor(x * (1.0 / 289.0)) * 289.0;
}

float3 perm(float3 x)
{
    return mod289(((x * 34.0) + 1.0) * x);
}

float4 noise3d(float3 p1, float3 p2, float3 p3, float3 p4)
{
    p1 = perm(perm(perm(p1) + p1.zxy) + p1.yzx);
    p2 = perm(perm(perm(p2) + p2.zxy) + p2.yzx);
    p3 = perm(perm(perm(p3) + p3.zxy) + p3.yzx);
    p4 = perm(perm(perm(p4) + p4.zxy) + p4.yzx);
    return frac(float4(p1.x, p2.x, p3.x, p4.x) / 41.0);
}

float perlin3d(float3 p)
{
    float3x3 r = float3x3(0.36, 0.48, -0.8, -0.8, 0.60, 0.0, 0.48, 0.64, 0.60);
    p = mul(r, p);

    float3 a = floor(p);
    float3 d = p - a;
    d = d * d * (3.0 - 2.0 * d);

    float4 o1 = noise3d(a,
                        a + float3(0.0, 1.0, 0.0),
                        a + float3(0.0, 0.0, 1.0),
                        a + float3(0.0, 1.0, 1.0));
    float4 o2 = noise3d(a + float3(1.0, 0.0, 0.0),
                        a + float3(1.0, 1.0, 0.0),
                        a + float3(1.0, 0.0, 1.0),
                        a + float3(1.0, 1.0, 1.0));

    float4 o3 = o2 * d.x + o1 * (1.0 - d.x);
    float2 o4 = o3.yw * d.y + o3.xz * (1.0 - d.y);

    return o4.y * d.z + o4.x * (1.0 - d.z);
}

void main(in float2 pass_Position : TEXCOORD0,
          uniform float u_Time,
          out float4 out_FragColor : COLOR)
{
    float2 xy = pass_Position;
    xy.y -= u_Time * 400.0;
    float z = u_Time * 2.0;

    float p = perlin3d(float3(xy / 80.0, z + 1.5)) / 2.0
            + perlin3d(float3(xy / 40.0, z + 0.3)) / 4.0
            + perlin3d(float3(xy / 20.0, z + 3.3)) / 8.0
            + perlin3d(float3(xy / 10.0, z + 2.0)) / 16.0;

    /* Scroll by adding [-.5,.5] */
    p -= pass_Position.y / 1440.0;
    p = max(p, 0.0);
    p = min(p, 1.0);

    float q = p * p * (3.0 - 2.0 * p);
    float r = q * q * (3.0 - 2.0 * q);
    out_FragColor = float4(min(q * 2.0, 1.0),
                           max(r * 1.5 - 0.5, 0.0),
                           max(q * 8.0 - 7.3, 0.0),
                           1.0);
}
